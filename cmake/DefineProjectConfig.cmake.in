if (CMAKE_VERSION VERSION_LESS 3.0)
    message(FATAL_ERROR "@PROJECT_NAME@ requires at least CMake version 3.0")
endif ()

set(_@PROJECT_NAME@_FIND_PARTS_REQUIRED)
if (@PROJECT_NAME@_FIND_REQUIRED)
    set(_@PROJECT_NAME@_FIND_PARTS_REQUIRED REQUIRED)
endif ()
set(_@PROJECT_NAME@_FIND_PARTS_QUIET)
if (@PROJECT_NAME@_FIND_QUIETLY)
    set(_@PROJECT_NAME@_FIND_PARTS_QUIET QUIET)
endif ()

get_filename_component(_@PROJECT_NAME@_install_prefix "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)

set(_@PROJECT_NAME@_NOTFOUND_MESSAGE)

set(_@PROJECT_NAME@_CMAKE_PREFIX_PATH_old ${CMAKE_PREFIX_PATH})
set(CMAKE_PREFIX_PATH ${_@PROJECT_NAME@_install_prefix})

set(module @LIBRARY_TARGET_NAME@)
find_package(@PROJECT_NAME@${module}
             ${_@PROJECT_NAME@_FIND_PARTS_QUIET}
             ${_@PROJECT_NAME@_FIND_PARTS_REQUIRED}
             PATHS "${_@PROJECT_NAME@_install_prefix}" NO_DEFAULT_PATH
             )
if (NOT @PROJECT_NAME@${module}_FOUND)
    if (@PROJECT_NAME@_FIND_REQUIRED_${module})
        set(_@PROJECT_NAME@_NOTFOUND_MESSAGE
            "${_@PROJECT_NAME@_NOTFOUND_MESSAGE}Failed to find @PROJECT_NAME@ component \"${module}\" config file at \"${_@PROJECT_NAME@_install_prefix}/@PROJECT_NAME@${module}/@PROJECT_NAME@${module}Config.cmake\"\n")
    elseif (NOT @PROJECT_NAME@_FIND_QUIETLY)
        message(WARNING "Failed to find @PROJECT_NAME@ component \"${module}\" config file at \"${_@PROJECT_NAME@_install_prefix}/@PROJECT_NAME@${module}/@PROJECT_NAME@${module}Config.cmake\"")
    endif ()
endif ()

# For backward compatibility set the LIBRARIES variable
list(APPEND @PROJECT_NAME@_LIBRARIES "@PROJECT_NAME@::${module}")

# For backward compatibility set the INCLUDE_DIR(s) variables
list(APPEND @PROJECT_NAME@_INCLUDE_DIR "@CMAKE_INSTALL_PREFIX@/include")
list(APPEND @PROJECT_NAME@_INCLUDE_DIRS "@CMAKE_INSTALL_PREFIX@/include")

# Restore the original CMAKE_PREFIX_PATH value
set(CMAKE_PREFIX_PATH ${_@PROJECT_NAME@_CMAKE_PREFIX_PATH_old})

if (_@PROJECT_NAME@_NOTFOUND_MESSAGE)
    set(@PROJECT_NAME@_NOT_FOUND_MESSAGE "${_@PROJECT_NAME@_NOTFOUND_MESSAGE}")
    set(@PROJECT_NAME@_FOUND False)
endif ()
